import distutils.sysconfig, os, sys
import KCore.Dist as Dist
#==============================================================================
# KBridge requires:
# C++ compiler
# Numpy
# KCore
# Cassiopee/Kernel
#==============================================================================

# Get prefix from command line
prefix = ARGUMENTS.get('prefix', '')
installPath = Dist.getInstallPath(prefix)

# Get compilers from Distutils ================================================
(cc, cxx, opt, basecflags, ccshared, ldshared, so_ext) = Dist.getDistUtilsCompilers()

# Python ======================================================================
(pythonVersion, pythonIncDir, pythonLibDir, pythonLibs) = Dist.checkPython()

# Test if numpy exists =======================================================
(numpyVersion, numpyIncDir, numpyLibDir) = Dist.checkNumpy()

# Test if kcore exists =======================================================
(kcoreVersion, kcoreIncDir, kcoreLibDir) = Dist.checkKCore()

# Is Cartesian solver present? ================================================
(Cassiopee, CassiopeeIncDir, CassiopeeLibDir, CassiopeeUseMpi) = Dist.checkCassiopee()
if (Cassiopee == False):
    print "Error: Cassiopee solver must be installed for compiling KBridge."
    sys.exit()

# Setting libraryDirs and libraries ===========================================
libraryDirs = [pythonLibDir, kcoreLibDir]
libraries = ["kcore"]
from KCore.config import *
(ok, libs, paths) = Dist.checkCppLibs([], additionalLibPaths)
libraryDirs += paths ; libraries += libs

# Env =========================================================================
import srcs
if Dist.getSystem()[0] == 'mingw': tools = ['mingw']
else: tools = None
AR = Dist.getAR()
env = Environment(tools=tools, CC=cc, CXX=cxx, AR=AR, CCFLAGS=[], CXXFLAGS=[],
                  LIBPATH=additionalLibPaths+libraryDirs,
                  LIBS=libraries+additionalLibs,
                  CPPPATH=["KBridge"]+additionalIncludePaths+[pythonIncDir, numpyIncDir, kcoreIncDir],
                  SHLIBSUFFIX=so_ext, SHLIBPREFIX='',
                  LINKFLAGS=Dist.getLinkArgs(),
                  ENV=Dist.getEnvForScons())
env.Replace(CPPFLAGS=Dist.getCppArgs())

#==============================================================================

myMod = env.StaticLibrary('kBridge', srcs.cpp_srcs)

if (CassiopeeUseMpi == True): env.Append(LIBS=['elsAc', 'mpi'])
else:  env.Append(LIBS=['elsAc'])
env.Append(LIBPATH=[CassiopeeLibDir])
env.Append(CPPPATH=[CassiopeeIncDir])
modDir = installPath+'/KBridge/'
dp1 = env.Install(modDir, myMod)
dp2 = env.Install(modDir, Glob('KBridge/*.py'))

env.Alias(target="install", source=[dp1,dp2])
